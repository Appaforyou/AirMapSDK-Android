apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.airmap.airmapsdk'
def libVersion = '4.0.0-alpha.1'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "$libVersion"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.10.0"
    testImplementation "org.robolectric:robolectric:3.4.2"
    
    // support
    def supportLibraryVersion = '27.1.1'
    api "com.android.support:appcompat-v7:$supportLibraryVersion"
    api "com.android.support:design:$supportLibraryVersion"
    api "com.android.support:cardview-v7:$supportLibraryVersion"
    api "com.android.support:recyclerview-v7:$supportLibraryVersion"
    api "com.android.support:support-annotations:$supportLibraryVersion"
    api "com.android.support:support-fragment:$supportLibraryVersion"
    api "com.android.support:support-v13:$supportLibraryVersion"

    // play services
    def playServicesVersion = '15.0.1'
    api "com.google.android.gms:play-services-location:$playServicesVersion"

    // mapbox
    api 'com.mapbox.mapboxsdk:mapbox-android-sdk:8.4.0'
    api 'com.mapbox.mapboxsdk:mapbox-android-plugin-places-v8:0.9.0'
    api 'com.mapbox.mapboxsdk:mapbox-sdk-services:4.9.0'
    api 'com.mapbox.mapboxsdk:mapbox-sdk-turf:4.9.0'

    // shared libraries
    api 'joda-time:joda-time:2.9.9'
    api 'io.reactivex:rxandroid:1.2.1'
    api 'com.googlecode.libphonenumber:libphonenumber:8.8.2'
    api 'online.devliving:securedpreferencestore:0.3.1' // Newer version available, but updating requires making some api changes within our code
    api 'net.openid:appauth:0.7.1'

    // other
    api 'com.squareup.okhttp3:okhttp:3.12.0'
    api 'com.jakewharton.timber:timber:4.6.0'
    api 'com.auth0.android:jwtdecode:1.1.1'
    api 'com.google.protobuf:protobuf-java:3.5.0'
    api 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
    api 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
}

configurations {
    // to avoid double inclusion of support libraries
    all*.exclude group: 'com.android.support', module: 'support-v4'
}

def siteUrl = 'http://www.airmap.com'
def projectName = 'com.airmap.airmapsdk'
def gitUrl = 'https://github.com/airmap/AirMapSDK-Android.git'

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        userOrg = 'airmapio'
        version libVersion
        name = projectName
        desc = 'Create Flights, Send Telemetry Data, Get Realtime Traffic Alerts.'
        licenses = ['MIT']
        labels = ['android', 'airmap', 'drone', 'airspace', 'sdk', 'aar']
        vcsUrl = gitUrl
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name projectName
            url siteUrl
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://github.com/airmap/AirMapSDK-Android/blob/master/LICENSE'
                }
            }
            developers {
                developer {
                    id 'collin'
                    name 'Collin Vance'
                    email 'collin@airmap.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }.writeTo("$buildDir/pom.xml")
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
